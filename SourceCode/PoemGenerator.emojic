💭 Protocols

🐊 🖨 🍇
	❗️📝➡️🍨🐚 🔡🍆
🍉 

🐊 💫 🍇
	❗️📇 ➡️ 🍨🐚 🔡🍆
🍉 

💭 Value Types


🕊 🌌 🍇
	🖍🆕 stylistic_elements 🍨🐚💫🍆

	🆕 🍼 stylistic_elements 🍨🐚💫🍆  🍇🍉

	❗️🧮 ➡️ 🍨🐚🔢🍆 🍇
		↩️ 🍨 3 11🍆
	🍉	

	❗️🎚 ➡️ 🔢 🍇
		↩️ 3
	🍉	

	❗️🧭 ➡️ 🔢 🍇
		↩️ 🐔stylistic_elements❗️
	🍉	

	❗️🎎 ➡️ 🍨🐚💫🍆 🍇
		↩️ stylistic_elements
	🍉	
🍉

🕊 ✒️ 🍇
	🖍🆕 value 🔡
	🖍🆕 styles 🌌
	
	🆕 🍼 styles 🌌 🍇
	
		🆕🎰🆕❗️ ➡️ random

		🔢 random 1 🧭styles❗️❗️ ➖ 1 ➡️ style_number

		🔢 random 1 🎚styles❗️❗️ ➖ 1 ➡️ theme_number

		🎎 styles❗️ ➡️ stylistic_elements

		🐽stylistic_elements style_number❗️ ➡️ stylistic_element

		📇 stylistic_element❗️ ➡️ theme_elements
		
		🐽theme_elements theme_number❗️ ➡️ 🖍value
	🍉

	❗️🆚➡️🔡🍇
		↩️ value
	🍉
🍉

🕊 📜 🍇
	🐊 🖨

	🖍🆕 styles 🌌
	🖍🆕 verses 🍨🐚✒️🍆

	🔐🆕 🍼 styles 🌌 🍼 verses 🍨🐚✒️🍆  🍇🍉

	🆕 ✍️  🍼 styles 🌌 max_random_bonduary 🔢 🍇
		🆕🎰🆕❗️ ➡️ random

		🔢 random 1 max_random_bonduary ❗️ ➡️ verses_number
		
		🆕🍨🐚✒️🍆🐴 verses_number❗️ ➡️ 🖍verses

		🔂 i 🆕⏩⏩ 0 verses_number ❗️  🍇
			🆕✒️🆕 styles❗️➡️ verse
 			🐻verses verse❗️
		🍉
	🍉

	❗️📝➡️🍨🐚 🔡🍆🍇
		
		🐔verses❗️ ➡️ verses_count

		🆕🍨🐚🔡🍆🐴 verses_count❗️ ➡️ 🖍🆕verses_value
		
		🔂 i 🆕⏩⏩ 0 verses_count ❗️  🍇
			🐽verses i❗️➡️ verse
			🆚verse❗️➡️ verse_value
			🐻verses_value verse_value❗️
		🍉

		↩️ verses_value
	🍉
🍉

🕊 📖 🍇
	🐊 🖨

	🖍🆕 styles 🌌
	🖍🆕 strophes 🍨🐚📜🍆

	🔐🆕 🍼 styles 🌌 🍼 strophes 🍨🐚📜🍆 🍇🍉 

	🆕 ✍️ 🍼 styles 🌌  🍇
		🆕🎰🆕❗️ ➡️ random

		🧮 styles❗️ ➡️ bonduary
		
		🔢 random 1 🐽bonduary 1❗️ ❗️ ➡️ strophes_number

		🆕🍨🐚📜🍆🐴 strophes_number❗️ ➡️ 🖍strophes

		🔂 i 🆕⏩⏩ 0 strophes_number ❗️  🍇
			🆕📜✍️ styles 🐽bonduary 0❗️❗️➡️ strophe
 			🐻strophes strophe❗️
		🍉
	🍉 

	❗️📝➡️🍨🐚 🔡🍆🍇
		
		🆕🍨🐚🔡🍆🐸❗️ ➡️ 🖍🆕verses_value

		🔂 i 🆕⏩⏩ 0 🐔strophes❗️ ❗️  🍇
			🐽strophes i❗️➡️ strophe
			
			📝strophe❗️ ➡️ verses_of_strophe

			🔂 j 🆕⏩⏩ 0 🐔verses_of_strophe❗️ ❗️  🍇
				🐽verses_of_strophe j❗️➡️ verse
				🐻verses_value verse❗️
			🍉
			
			🐻verses_value 🔤🔤❗️
		🍉
		↩️ verses_value
	🍉
🍉

💭 Value Types - Data Source

🕊 🧚 🍇
  	🐊 💫

	🖍🔐🆕 elements 🍨🐚 🔡🍆 ⬅️ 🍨 🔤Casino, conosci i miei abiti🔤 🔤Bici, compagna di vita🔤 🔤Freddo, schiacci le ossa🔤🍆
	
	🆕 🍇🍉

	❗️📇 ➡️ 🍨🐚 🔡🍆 🍇
		↩️ elements
	🍉
🍉

🕊 😱 🍇
  	🐊 💫

	🖍🔐🆕 elements 🍨🐚 🔡🍆 ⬅️ 🍨 🔤Ed io, caro amico, ti evito🔤 🔤Ti cerco, riparatore di freni🔤 🔤Nonno cardatore, ti ricordo🔤🍆
	
	🆕  🍇🍉

	❗️📇 ➡️ 🍨🐚 🔡🍆 🍇
		↩️ elements
	🍉
🍉

🕊 🤪 🍇
  	🐊 💫

	🖍🔐🆕 elements 🍨🐚 🔡🍆 ⬅️ 🍨 🔤Hei, tu, sei ancora qua?🔤 🔤Perchè ho bisogno di te?🔤 🔤Funziona una coperta?🔤🍆
	
	🆕  🍇🍉

	❗️📇 ➡️ 🍨🐚 🔡🍆 🍇
		↩️ elements
	🍉
🍉

🕊 😲 🍇
  	🐊 💫

	🖍🔐🆕 elements 🍨🐚 🔡🍆 ⬅️ 🍨 🔤Oh, mie responsabilità!🔤 🔤Spingi! Spingi! Spingi!🔤 🔤Ah, sempre peggio!🔤🍆
	
	🆕  🍇🍉

	❗️📇 ➡️ 🍨🐚 🔡🍆 🍇
		↩️ elements
	🍉
🍉


💭 Main Block

🏁 🍇

	🆕🌌🆕 🍨 🆕🧚🆕❗️🆕😱🆕❗️🆕🤪🆕❗️🆕😲🆕❗️🍆❗️ ➡️ milky_way

	🆕📖✍️ milky_way❗️ ➡️ poem
	
	📝poem❗️ ➡️ printable_verses
	
	🔂 i 🆕⏩⏩ 0 🐔printable_verses❗️ ❗️
	 🍇
		🐽printable_verses i❗️➡️ printable_verse
		😀printable_verse❗️
	🍉
🍉